/*
 * Copyright Â© 2008 Keith Packard
 *
 * This library is free software; you can redistribute it and/or
 * modify it either under the terms of the GNU Lesser General Public
 * License version 2.1 as published by the Free Software Foundation
 * (the "LGPL") or, at your option, under the terms of the Mozilla
 * Public License Version 1.1 (the "MPL"). If you do not alter this
 * notice, a recipient may use your version of this file under either
 * the MPL or the LGPL.
 *
 * You should have received a copy of the LGPL along with this library
 * in the file COPYING-LGPL-2.1; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
 * You should have received a copy of the MPL along with this library
 * in the file COPYING-MPL-1.1
 *
 * The contents of this file are subject to the Mozilla Public License
 * Version 1.1 (the "License"); you may not use this file except in
 * compliance with the License. You may obtain a copy of the License at
 * http://www.mozilla.org/MPL/
 *
 * This software is distributed on an "AS IS" basis, WITHOUT WARRANTY
 * OF ANY KIND, either express or implied. See the LGPL or the MPL for
 * the specific language governing rights and limitations.
 *
 * The Original Code is the cairo graphics library.
 *
 * The Initial Developer of the Original Code is Keith Packard
 *
 * Contributor(s):
 *      Keith Packard <keithp@keithp.com>
 */
autoload Chrome;

extend namespace Chrome {
    public namespace Button {

	public typedef button_t;

	public typedef struct {
	    real x, y;
	    real pad;
	    real width, height;
	    real preferred_width, preferred_height;
	} metrics_t;
	
	public typedef void (&widget_t widget, bool state) callback_t;
	public typedef metrics_t (cairo_t cr, &button_t widget) get_metrics_t;

	public typedef widget_t + struct {
	    string  label;
	    string  font;
	    bool    pressed;
	    bool    inside;
	    real    pad_ratio;
	    rgba_color_t    color;
	    callback_t	callback;
	    get_metrics_t get_metrics;
	} button_t;
	
	public metrics_t get_metrics (cairo_t cr, &button_t widget) {
	    set_font (cr, widget.font);
	    text_extents_t text = text_extents (cr, widget.label);
	    font_extents_t font = font_extents(cr);
	    metrics_t metrics;
	    
	    real pad = font.height / 2;
	    metrics.pad = pad;
	    metrics.preferred_width = pad + text.width + pad;
	    metrics.preferred_height = pad + font.height + pad;
	    metrics.width = max (widget.geometry.width, metrics.preferred_width);
	    metrics.height = max (widget.geometry.height, metrics.preferred_height);
	    metrics.y = (metrics.height - font.ascent) / 2 + font.ascent;
	    metrics.x = (metrics.width - text.width) / 2;
	    return metrics;
	}
	
	public void natural (cairo_t cr, &button_t widget) {
	    metrics_t	metrics = widget.get_metrics (cr, &widget);

	    rectangle (cr, 0, 0, metrics.preferred_width, metrics.preferred_height);
	}
	
	public void outline (cairo_t cr, &button_t widget) {
	    metrics_t	metrics = widget.get_metrics (cr, &widget);

	    rectangle (cr, 0, 0, metrics.width, metrics.height);
	}
	
	public void draw (cairo_t cr, &button_t widget) {
	    metrics_t	metrics = widget.get_metrics (cr, &widget);
	    
	    if (widget.pressed && widget.inside)
	    {
		rectangle (cr, 0, 0, metrics.width, metrics.height);
		set_source_rgba (cr, widget.color.red,
				 widget.color.green, widget.color.blue,
				 widget.color.alpha);
		fill (cr);
	    }
	    if (widget.inside && !widget.pressed) {
		set_source_rgba (cr, widget.color.red,
				 widget.color.green, widget.color.blue,
				 widget.color.alpha * 0.6);
		rectangle (cr, metrics.pad / 2, metrics.pad / 2,
			   metrics.width - metrics.pad, metrics.height -
			   metrics.pad);
		fill (cr);
	    }
	    move_to (cr, metrics.x, metrics.y);
	    set_source_rgb (cr, 0, 0, 0);
	    show_text (cr, widget.label);
	}
	
	public void relabel (&button_t widget, string label) {
	    widget.label = label;
	    Widget::resize (&widget);
	}

	public void button (&button_t widget, &button_event_t event) {
	    enum switch (event.type) {
	    case press:
		widget.pressed = true;
		Widget::redraw (&widget);
		break;
	    case release:
		Widget::redraw (&widget);
		widget.pressed = false;
		if (widget.inside)
		    widget.callback (&widget, true);
		break;
	    default:
		break;
	    }
	}
	
	public void motion (&button_t widget, &motion_event_t event) {
	    enum switch (event.type) {
	    case leaving:
		widget.inside = false;
		Widget::redraw (&widget);
		break;
	    case entering:
		widget.inside = true;
		Widget::redraw (&widget);
		break;
	    case inside:
	    case outside:
		break;
	    }
	}

	public void init (&button_t widget,
			  &chrome_t chrome,
			  string label,
			  callback_t callback) {
	    Widget::init (&chrome, &widget);
	    widget.label = label;
	    widget.font = Widget::default_font;
	    widget.draw = draw;
	    widget.pressed = false;
	    widget.inside = false;
	    widget.button = button;
	    widget.motion = motion;
	    widget.callback = callback;
	    widget.outline = outline;
	    widget.natural = natural;
	    widget.get_metrics = get_metrics;
	    widget.pad_ratio = 1;
	    widget.color = Widget::default_color;
	}
	
	public *widget_t new (&chrome_t chrome,
			      string label,
			      callback_t callback) {
	    button_t	widget;

	    init (&widget, &chrome, label, callback);
	    return &widget;
	}
    }
}
